buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'org.hidetake.ssh' version '2.8.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'


version = "0.1.0" + "-" + System.env.TRAVIS_BRANCH + "-r" + System.env.TRAVIS_BUILD_NUMBER
group = "net.qbar"
archivesBaseName = "qbar"

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12-14.21.1.2395"
    runDir = "run"

    mappings = "snapshot_20170624"
}

repositories {
    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }

    maven { // JEI
        name = 'progwml6 maven'
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven { // Concrete
        name = 'elytradev'
        url = "http://repo.elytradev.com"
    }
    maven { // TeaBeans
        name = 'ourten'
        url = 'http://maven.ferenyr.info/artifactory/ourten'
    }
    maven { // BrokkGUI
        name = 'yggard'
        url = 'http://maven.ferenyr.info/artifactory/yggard'
    }
    maven { // RedstoneFlux
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = 'fat'
    relocate 'com.elytradev.concrete', 'net.qbar.com.elytradev.concrete'
    configurations = [project.configurations.shadow]
    dependencies {
        exclude(dependency {
            it.moduleGroup == 'com.google.guava' || it.moduleGroup == 'org.apache.commons' || it.moduleGroup == "com.google.code.findbugs"
        })
    }
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

dependencies {
    deobfCompile 'mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.11-5'
    deobfCompile 'mezz.jei:jei_1.12:4.7.1.69'

    compile 'org.yggard:brokkgui-binding-mc1.12:0.1.+'
    shadow 'org.yggard:brokkgui-binding-mc1.12:0.1.+'

    compile 'org.hjson:hjson:1.0.0'
    shadow 'org.hjson:hjson:1.0.0'

    compile 'com.elytradev:concrete:0.3.1:common'
    shadow 'com.elytradev:concrete:0.3.1:common'

    compile 'com.elytradev:concrete:0.3.1:reflect'
    shadow 'com.elytradev:concrete:0.3.1:reflect'

    compile 'com.elytradev:concrete:0.3.1:network'
    shadow 'com.elytradev:concrete:0.3.1:network'

    testCompile 'org.assertj:assertj-core-java8:1.0.0m1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.47'
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

apply plugin: 'org.hidetake.ssh'

remotes {
    prodServer {
        retryCount = 3
        retryWaitSec = 10
        knownHosts = allowAnyHosts
        host = System.env.BUILD_SSH_HOST
        user = System.env.BUILD_SSH_USER
        port = (System.env.BUILD_SSH_PORT ?: "0") as Integer
        password = System.env.BUILD_SSH_PASSWD
    }
}

task deploy(dependsOn: "build") << {
    ssh.run {
        session(remotes.prodServer) {
            put from: file('build/libs/'), into: '/home/games/minecraft/dev/'
        }
    }
}