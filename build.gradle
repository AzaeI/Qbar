buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'net.minecraftforge.gradle.forge'


version = "0.0.1"
group= "net.qbar" 
archivesBaseName = "qbar"

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.11.2-13.20.0.2233"
    runDir = "run"

    mappings = "snapshot_20161220"
}

repositories {
    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }

    maven { // JEI
	   name = 'progwml6 maven'
        url = "http://dvs1.progwml6.com/files/maven"
	}
	maven { // Concrete
		name = 'unascribed'
        url = 'http://unascribed.com/maven/releases'
    }
    maven { // TeaBeans
    	name = 'ourten'
    	url = 'http://maven.ferenyr.info/artifactory/ourten'
    }
    maven { // BrokkGUI
    	name = 'yggard'
    	url = 'http://maven.ferenyr.info/artifactory/yggard'
	}
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    relocate 'io.github.elytra.concrete', 'net.qbar.io.github.elytra.concrete'
    configurations = [project.configurations.shadow]
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

dependencies {
	deobfCompile "mcjty.theoneprobe:TheOneProbe:1.11-1.3.4-47"
	deobfCompile "mezz.jei:jei_1.11.2:4.2.2.220"
	deobfCompile 'org.yggard:brokkgui-binding-mc1.11.2:0.1.+'
	compile 'io.github.elytra:concrete:0.0.6'
    shadow 'io.github.elytra:concrete:0.0.6'
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
