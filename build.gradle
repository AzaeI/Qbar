buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'org.hidetake.ssh' version '2.8.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'


version = "0.1.0" + "-" + System.env.TRAVIS_BRANCH +"-r" + System.env.TRAVIS_BUILD_NUMBER
group= "net.qbar" 
archivesBaseName = "qbar"

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.11.2-13.20.0.2233"
    runDir = "run"

    mappings = "snapshot_20161220"
}

repositories {
    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }

    maven { // JEI
	   name = 'progwml6 maven'
        url = "http://dvs1.progwml6.com/files/maven"
	}
	maven { // Concrete
		name = 'elytradev'
        url = "http://repo.elytradev.com"
    }
    maven { // TeaBeans
    	name = 'ourten'
    	url = 'http://maven.ferenyr.info/artifactory/ourten'
    }
    maven { // BrokkGUI
    	name = 'yggard'
    	url = 'http://maven.ferenyr.info/artifactory/yggard'
	}
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = 'fat'
    relocate 'com.elytradev.concrete', 'net.qbar.com.elytradev.concrete'
    configurations = [project.configurations.shadow]
    dependencies {
       exclude(dependency {
            it.moduleGroup == 'com.google.guava' || it.moduleGroup == 'org.apache.commons' || it.moduleGroup == "com.google.code.findbugs" 
       })
   }
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

dependencies {
	deobfCompile 'mcjty.theoneprobe:TheOneProbe:1.11-1.3.4-47'
	deobfCompile 'mezz.jei:jei_1.11.2:4.2.8.243'
    compile 'org.yggard:brokkgui-binding-mc1.11.2:0.2.+'
	shadow 'org.yggard:brokkgui-binding-mc1.11.2:0.2.+'
    compile 'com.elytradev:concrete:0.1.0'
    shadow 'com.elytradev:concrete:0.1.0'
    compile 'org.hjson:hjson:1.0.0'
    shadow 'org.hjson:hjson:1.0.0'
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

apply plugin: 'org.hidetake.ssh'

remotes {
  prodServer {
    retryCount = 3
    retryWaitSec = 10
    knownHosts = allowAnyHosts
    host = System.env.BUILD_SSH_HOST
    user = System.env.BUILD_SSH_USER
    port = (System.env.BUILD_SSH_PORT ?: "0") as Integer
    password = System.env.BUILD_SSH_PASSWD
  }
}

task deploy(dependsOn: "build") << {
  ssh.run {
    session(remotes.prodServer) {
        put from: file('build/libs/'), into: '/home/games/minecraft/dev/'
    }
  }
}